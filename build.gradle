
buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
  }
}

plugins {
  id 'java'
  id 'groovy'
  id 'maven-publish'
}

def mavenUrl = System.getProperty("maven.url")
def mavenUser = System.getProperty("maven.user")
def mavenPwd = System.getProperty("maven.password")

task release {
}

subprojects {

  apply {
    plugin 'java'
    plugin 'groovy'
    plugin 'maven-publish'
    plugin 'com.jfrog.bintray'
  }

  version = 0.66
  group = 'org.egripse'

  repositories {
    mavenCentral()
    maven {
      url 'http://www.fornax-platform.org/m2/repository'
    }
  }

  dependencies {
    compile localGroovy()
    compile gradleApi()
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:18.0'
    testCompile 'commons-io:commons-io:2.4'

  }

  sourceCompatibility = 1.8

  //create org.eclipse.equinox.simpleconfigurator jar
  tasks.create('sourceJar', Jar) {
    from sourceSets.main.allJava
  }

  //publish the additional org.eclipse.equinox.simpleconfigurator jar
  publishing {
    repositories {
      maven {
        credentials {
          username mavenUser
          password mavenPwd
        }
        url mavenUrl
      }
    }
    publications {
      myPublication(MavenPublication) {
        from components.java

        artifact project.sourceJar {
          classifier 'sources'
        }
      }
    }
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourcesJar
  }

  dependencies {
    compile gradleApi()
    compile localGroovy()
  }

  //Deploy on bintray
  apply {
    plugin 'com.jfrog.bintray'
  }
  bintray {
    user = System.properties['bintray.username']
    key = System.properties['bintray.password']
    publications = ['myPublication']
    pkg {
      repo = 'egripse'
      name = "${project.name}"
      desc = 'gradle plugins to build eclipse bundles and features'
      websiteUrl = 'https://github.com/moley/egripse'
      issueTrackerUrl = 'https://github.com/moley/egripse'
      vcsUrl = 'https://github.com/moley/egripse.git'
      licenses = ['Apache-2.0']
      labels = ['eclipse', 'gradle', 'gorilla']
      attributes= ['plat': ['win', 'linux', 'osx']]
      publicDownloadNumbers = true
      version.attributes = [ 'gradle-plugin': "org.egripse.${project.name}:org.egripse:${project.name}" ]
    }
  }

}

allprojects {
  version = project.rootProject.file('releasenotes.md').text.split("\n").first().replaceAll("\\*", "")
  test {
    testLogging {
      events "passed", "skipped", "failed"
      exceptionFormat 'full'
      showStackTraces = 'true'
    }
  }
}

project (':eclipsebase') {
  dependencies {
    compile 'de.pdark:decentxml:1.3'
    compile 'com.diffplug.gradle:goomph:3.17.1'
  }

  rootProject.tasks.release.dependsOn tasks.bintrayUpload


}

project (':eclipsefeature') {

  dependencies {
    compile project (":eclipsebase")
  }

  rootProject.tasks.release.dependsOn tasks.bintrayUpload
}

project (':eclipseheadless') {
  dependencies {
    compile project(':eclipsebase')
  }

  rootProject.tasks.release.dependsOn tasks.bintrayUpload
}

project (':eclipsemwe') {

  repositories {
    maven { url "http://fornax.itemis.de/nexus/content/repositories/eclipse-maven/" }
  }

  dependencies {
    compile project(':eclipseplugin')
    compile project(':eclipsebase')
  }
  rootProject.tasks.release.dependsOn tasks.bintrayUpload

}

project (':eclipseplugin') {
  dependencies {
    compile project (":eclipsebase")
  }
  rootProject.tasks.release.dependsOn tasks.bintrayUpload
}

project (':eclipsesurefire') {
  dependencies {
    compile project(':eclipseheadless')
  }
  rootProject.tasks.release.dependsOn tasks.bintrayUpload
}

project (':integrationtests') {

  dependencies {
    testCompile gradleTestKit()
    testCompile project (":eclipsebase")
    testCompile project (":eclipseplugin")
    testCompile project (":eclipsefeature")
    testCompile project (":eclipsemwe")
  }


  //package performance is used for extra performance test task
  project.tasks.findByName('test').exclude '**/integrationtest/**'
  tasks.create('integrationtest', Test) {
    outputs.upToDateWhen {false}
    dependsOn(rootProject.getTasksByName('publishToMavenLocal', true))
    include '**/integrationtest/**'
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests'
    testLogging {
      events "passed", "skipped", "failed"
      exceptionFormat 'full'
      showStackTraces = 'true'
    }
  }
}

println "Using repo url " + mavenUrl + " with user " + mavenUser + " and version " + version


